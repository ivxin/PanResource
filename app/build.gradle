apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

static def releaseTime() {
    return new Date().format("yyMMddHHmm", TimeZone.getDefault())
}

android {
    compileSdkVersion 31
    buildToolsVersion '30.0.3'
    defaultConfig {
        applicationId "com.ivxin.panresource"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 2
        versionName "v"+releaseTime()
    }
    signingConfigs {
        //加载资源
        Properties properties = new Properties()
        InputStream inputStream = project.rootProject.file('local.properties').newDataInputStream()
        properties.load(inputStream)
        //读取字段
        def debugKeyPath = properties.getProperty('debugkey.path')
        def keystorePath = properties.getProperty('keystore.path')
        def keystorePSW = properties.getProperty('keystore.password')
        def keystoreAlias = properties.getProperty('keystore.alias')
        def keystoreAliasPSW = properties.getProperty('keystore.alias_password')
        debug {
            storeFile file(debugKeyPath)
            storePassword keystorePSW
            keyAlias keystoreAlias
            keyPassword keystoreAliasPSW
        }

        release {
            storeFile file(keystorePath)
            storePassword keystorePSW
            keyAlias keystoreAlias
            keyPassword keystoreAliasPSW
            v2SigningEnabled false
        }
    }

    buildTypes {
        debug {
            // 显示Log
            buildConfigField "boolean", "LOG_DEBUG", "true"
            zipAlignEnabled false
            signingConfig signingConfigs.debug
        }
        release {
            proguardFiles 'proguard-rules.txt'
            // 不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"
            //是否支持Zip Align
            zipAlignEnabled true
            // 移除无用的resource文件
//            minifyEnabled true
//            shrinkResources true
            //是否支持本地化整理
            pseudoLocalesEnabled true
            signingConfig signingConfigs.release
            android.applicationVariants.all { variant ->
                variant.outputs.all {
                    // 输出apk名称为panresource_v1.0_2015-01-15.apk
                    outputFileName = "panresource_${defaultConfig.versionName}.apk"
                }
            }
        }
    }
    buildFeatures {
        dataBinding true
        viewBinding true
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/plexus/components.xml'

    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'org.jsoup:jsoup:1.13.1'

    //https://github.com/Tencent/QMUI_Android
    implementation 'com.qmuiteam:qmui:2.0.0-alpha10'
    //https://github.com/hzy3774/AndroidP7zip
    implementation 'com.hzy:libp7zip:1.7.0'
    //https://github.com/SusionSuc/DraggableImageViewer
    implementation 'com.susion:image-viewer:1.0.2'
    //Glide图片加载器 glide https://github.com/bumptech/glide
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    //百分比线性布局拓展
    implementation 'com.zhy:percent-support-extends:1.1.1'
    //https://github.com/mukeshsolanki/MarkdownView-Android
//    implementation 'com.github.mukeshsolanki:MarkdownView-Android:2.0.0'
    //https://github.com/falnatsheh/MarkdownView
    implementation 'us.feras.mdv:markdownview:1.1.0'
    //kotlin
    implementation "androidx.core:core-ktx:1.6.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2"
    //http://doc.bmob.cn/data/android/index.html#sdk_1
    implementation 'io.github.bmob:android-sdk:3.8.11'
    implementation "io.reactivex.rxjava2:rxjava:2.2.21"
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'com.squareup.okhttp3:okhttp:4.8.1'
    implementation 'com.squareup.okio:okio:2.7.0'

}
repositories {
    mavenCentral()
}
